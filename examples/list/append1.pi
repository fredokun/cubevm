
def ListNode(ctl,next,val) = 
  ctl?(x),//#print("Node ctl = "),#println(x),
  if x=0 then ctl!val,ListNode(*)   // getVal()
  else if x=1 then ctl!next,ListNode(*)  // getNext()
  else if x=2 then ctl!false,ListNode(ctl,next,val) // isNil()
  else if x=3 then ctl?(x),ListNode(ctl,x,val) // setNext()
  else if x=4 then ctl?(x),ListNode(ctl,next,val) // setVal()  
  else #print("Problem in ListNode : ctl = "),#println(x);

def ListNil(ctl) = 
  ctl?(x),if x=2 then ctl!true,ListNil(ctl) 
  else #println("Problem in ListNil : ctl "+ctl+" unknwon");

def ListNum(prev,n,m,END) = if n>m then [ END!1 || ListNil(prev)]
                        else new(next), 
                           [ ListNum(next,n+1,m,END) || ListNode(prev,next,n) ];

def ListPrintContents(list,END) = 
  list!2,list?(x),if x=false then list!0,list?(x),#print(x),
  list!1,list?(x),x!2,x?(y),if y=true then #println("]"),END!1
  else #print(","),ListPrintContents(x,END);

def ListPrint(list,END) = #print("["),ListPrintContents(list,END);

def ListAppend(list1,list2,END) = 
        list1!2,list1?(x),if x=true then END!1
        else list1!1,list1?(next1),next1!2,next1?(isnil),
        if isnil=true then list1!3,list1!list2,END!1
        else ListAppend(next1,list2,END);

new(list1),new(list2),new(end1),new(end2),new(end3),new(end4),new(end5),new(end6),
  [ ListNum(list1,1,1000,end1) || 
    end1?(x),ListNum(list2,1001,2000,end2) ||
    end2?(x),#print("l1 = "),ListPrint(list1,end3) || 
    end3?(x),#print("l2 = "),ListPrint(list2,end4) || 
    end4?(x),ListAppend(list1,list2,end5) || 
    end5?(x),#print("l1+l2 = "),ListPrint(list1,end6) ||  
    end6?(x) ]

